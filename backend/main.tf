provider "azurerm" {
    features {}
}

resource "random_integer" "sa_num" {
    min = 1000
    max = 9000
}

resource "azurerm_resource_group" "setup" {
    name        = var.resource_group_name
    location    = var.location
}

resource "azurerm_storage_account" "sa" {
    name                        = "${lower(var.name_prefix)}${random_integer.sa_num.result}"
    resource_group_name         = azurerm_resource_group.setup.name
    location                    = var.location
    account_tier                = "Standard"
    account_replication_type    = "LRS"
}

resource "azurerm_storage_container" "tfstate" {
    name                    = "terraform-state"
    storage_account_name    = azurerm_storage_account.sa.name
}

data "azurerm_storage_account_sas" "state" {
    connection_string   = azurerm_storage_account.sa.primary_connection_string
    https_only          = true

    resource_types {
        service     = true
        container   = true
        object      = true
    }

    services {
        blob    = true
        queue   = false
        table   = false
        file     = false
    }

    start   = timestamp()
    expiry  = timeadd(timestamp(), "17521h")

    permissions {
        read    = true
        write   = true
        delete  = true
        list    = true
        add     = true
        create  = true
        update  = false
        process = false
    }
}

resource "null_resource" "post-config" {
    depends_on = [azurerm_storage_container.tfstate]

    provisioner "local-exec" {
        command = <<EOT
echo 'storage_account_name = "${azurerm_storage_account.sa.name}"' >> backend-config.txt
echo 'container_name = "terraform-state"' >> backend-config.txt
echo 'key = "terraform.tfstate"' >> backend-config.txt
echo 'sas_token = "${data.azurerm_storage_account_sas.state.sas}"' >> backend-config.txt
EOT
    }
}